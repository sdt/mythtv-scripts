#!/usr/bin/env perl

use strict;
use warnings;
use 5.12.0;

use DateTime;
use DateTime::Duration;
use DateTime::Format::Strptime;
use Getopt::Long qw( GetOptions );

my %opt = (
    help => 0,
    statefile => '/tmp/checkshutdown.state',
    syslog => 0,
);

my %check_table = (

    apache => make_logfile_checker(Apache =>
                '/var/log/apache2/access.log', 3 * 60),

    console_user => {
        help => 'Check is a user is logged in at the console',
        code => sub {
            return 'console user is active'
                unless grep { /mythwelcome/ } `ps ax`;
            return;
        },
    },

    mythshutdown => {
        help => 'Check if MythTV is busy',
        code => sub {
            my $exit = system(qw( mythshutdown --status ));
            return sprintf('mythshutdown status 0x%02x', $exit >> 8)
                if $exit > 0;
            return; # 0 = shutdown ok, -1 = didn't run
        },
    },

    sabnzbd => make_logfile_checker(SABnzbd =>
                '/home/sab/.sabnzbd/logs/sabnzbd.log', 5 * 60),

    sickbeard => make_logfile_checker(Sickbeard =>
                '/opt/sickbeard/Logs/sickbeard.log', 2 * 60),

    ssh_sessions => {
        help => 'Check for users logged in by ssh',
        code => sub {
            my $active_ssh_sessions =
                scalar grep { /:ssh$/ }
                       map { [ split ]->[3] }
                       `netstat --inet --numeric-hosts`;
            return "$active_ssh_sessions active ssh sessions"
                if $active_ssh_sessions;
            return;
        },
    },

    xbmc => {
        help => 'Check if XBMC is running',
        code => sub {
            return 'XBMC is running'
                if grep { m</usr/bin/xbmc> } `ps ax`;
            return;
        },
    },

    uptime => {
        help => 'Check is system has been up long enough',
        code => sub {
            my $minimum_minutes = 5;
            open(my $fh, '<', '/proc/uptime') or return;
            my $line = <$fh>;
            my ($uptime) = split(/\s+/, $line);
            return "up for less than $minimum_minutes minutes"
                unless $uptime > $minimum_minutes * 60;
            return;
        },
    },

);

my @default_checks = qw(
    apache
    console_user
    mythshutdown
    sabnzbd
    sickbeard
    ssh_sessions
    uptime
    xbmc
);

my $opt_ok = GetOptions(
    'help|h|?' => \$opt{help},
    'syslog|s' => \$opt{syslog},
);

if ($opt{help} || !$opt_ok) {
    usage();
}

exit main(@ARGV);

sub main {
    my @checks = @_ ? @_ : @default_checks;
    my $ok_to_shutdown = 1;
    my @msgs;

    for my $name (@checks) {
        my $check = $check_table{$name};
        if (!$check) {
            diag("Unknown check: $name");
            next;
        }
        my $msg = $check->{code}->();
        $ok_to_shutdown = 0 if $msg;
        push(@msgs, $msg) if $msg;
        $msg ||= 'OK';
        logit(sprintf("%-16s$msg", "$name")) unless $opt{syslog};
    }
    if ($opt{syslog}) {
        my $prev_state = load_state();
        my $new_state = $ok_to_shutdown ? 'all clear for shutdown' : 'busy: ' . join(', ', @msgs);
        if ($prev_state ne $new_state) {
            logit($new_state);
            save_state($new_state);
        }
    }
    else {
        logit($ok_to_shutdown ? 'all clear for shutdown' : 'not shutting down');
    }
    return $ok_to_shutdown ? 0 : 1;
}

sub usage {
    my $msg = "usage: $0 [--syslog] [check [checks...]]\n\nchecks:\n";
    my %check_enabled = map { $_ => 1 } @default_checks;
    for my $name (sort keys %check_table) {
        my $check = $check_table{$name};
        my $enabled = $check_enabled{$name} ? '* ' : '  ';
        $msg .= sprintf"\t%-16s%s\n", $name, $enabled . $check->{help};
    }
    die $msg;
}

sub diag {
    logit('info: ', @_);
    return;
}

sub load_state {
    open(my $fh, '<', $opt{statefile}) or return "";
    my $state = <$fh> || "";
    chomp $state;
    return $state;
}

sub save_state {
    my ($state) = @_;
    open(my $fh, '>', $opt{statefile}) or return;
    print {$fh} $state, "\n";
}

sub logit {
    if ($opt{syslog}) {
        system(qw( logger -p local0.info -t ), "checkshutdown[$$]", @_);
    }
    else {
        say join(' ', @_);
    }
}

sub check_last_mtime {
    my ($name, $filename, $min_age_sec) = @_;

    my $mtime_age = time - ((stat($filename))[9] // 0);
    return if ($mtime_age > $min_age_sec);
    return "$name access $mtime_age seconds ago within $min_age_sec seconds";
}

sub make_logfile_checker {
    my ($name, $filename, $min_age_sec) = @_;
    return {
        help => "Check if $name is busy",
        code => sub { check_last_mtime($name, $filename, $min_age_sec) },
    };
}

