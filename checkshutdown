#!/usr/bin/env perl

use strict;
use warnings;
use 5.10.1;

use Getopt::Long qw( GetOptions );
use IPC::Cmd qw( run );
use JSON::XS qw( decode_json );

my $verbose;

GetOptions(
    'verbose|v' => \$verbose,
);

my @checks = (
    \&check_sabnzbd,
    \&check_mythfrontend,
    \&check_mythshutdown,
    \&check_ssh_sessions,
    \&check_uptime,
);

exit main();

sub main {
    for my $check (@checks) {
        $check->();
    }
    logit('all clear for shutdown');
    return 0;
}

sub check_mythfrontend {
    no_shutdown('mythfrontend not running')
        unless grep { /mythfrontend/ } `ps ax`;
    return;
}

sub check_mythshutdown {
    no_shutdown('mythshutdown is busy')
        unless run(command => [qw( mythshutdown --status )]);
    return;
}

sub check_sabnzbd {
    my $host = 'localhost';
    my $port = 8888;
    my $key = '77f3b3e96a07fe2dca05a7fd45f4638b';
    my $uri = "http://$host:$port/sabnzbd/api?mode=qstatus\\&output=json\\&apikey=$key";

    my $sab = eval { decode_json(`curl -s $uri`) };
    diag("curl $uri failed: $@") unless $sab;

    no_shutdown("SABnzbd has $sab->{noofslots} active downloads")
        if $sab && ($sab->{noofslots} > 0) && !$sab->{paused};
    return;
}

sub check_ssh_sessions {
    my $active_ssh_sessions = scalar grep { /:ssh$/ }
                                     map { [ split ]->[3] }
                                     `netstat --inet --numeric-hosts`;
    no_shutdown("$active_ssh_sessions active ssh sessions")
        if $active_ssh_sessions;
    return;
}

sub check_uptime {
    my $minimum_minutes = 5;
    open(my $fh, '<', '/proc/uptime') or return;
    my $line = <$fh>;
    my ($uptime) = split(/\s+/, $line);
    no_shutdown("up for less than $minimum_minutes minutes")
        unless $uptime > $minimum_minutes * 60;
    return;
}

sub diag {
    logit('info: ', @_);
    return;
}

sub no_shutdown {
    logit('not ready for shutdown:', @_);
    exit 1;
}

sub logit {
    if ($verbose) {
        say join(' ', @_) if $verbose;
    }
    else {
        system(qw( logger -p local0.info -t ), "checkshutdown[$$]", @_);
    }
}
